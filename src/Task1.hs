{-# OPTIONS_GHC -Wall #-}

-- The above pragma enables all warnings

module Task1 where

import Data.Function ((&))
import Text.Read (readMaybe)

-- * Expression data type

-- | Representation of integer arithmetic expressions comprising
-- - Literals of type 'a'
-- - Binary operations 'Add' and 'Mul'
data IExpr
  = Lit Integer
  | Add IExpr IExpr
  | Mul IExpr IExpr
  deriving (Show)

-- * Evaluation

-- | Evaluates given 'IExpr'
--
-- Usage example:
--
-- >>> evalIExpr (Lit 2)
-- 2
-- >>> evalIExpr (Add (Lit 2) (Lit 3))
-- 5
-- >>> evalIExpr (Add (Mul (Lit 3) (Lit 2)) (Lit 3))
-- 9
evalIExpr :: IExpr -> Integer
evalIExpr (Lit value) = value
evalIExpr (Add lhs rhs) = (evalIExpr lhs) + (evalIExpr rhs)
evalIExpr (Mul lhs rhs) = (evalIExpr lhs) * (evalIExpr rhs)

-- * Parsing

-- | Class of parseable types
class Parse a where
  -- | Parses value 'a' from given string
  -- wrapped in 'Maybe' with 'Nothing' indicating failure to parse
  parse :: String -> Maybe a

instance Parse Integer where
  parse = readMaybe

-- | Parses given expression in Reverse Polish Notation
-- wrapped in 'Maybe' with 'Nothing' indicating failure to parse
--
-- Usage example:
--
-- >>> parse "2" :: Maybe IExpr
-- Just (Lit 2)
-- >>> parse "2 3 +" :: Maybe IExpr
-- Just (Add (Lit 2) (Lit 3))
-- >>> parse "3 2 * 3 +" :: Maybe IExpr
-- Just (Add (Mul (Lit 3) (Lit 2)) (Lit 3))
-- >>> parse "2 +" :: Maybe IExpr
-- Nothing
-- >>> parse "2 3" :: Maybe IExpr
-- Nothing
instance Parse IExpr where
  parse = result . (foldl' (&) (Just []) =<<) . sequence . (map parseStackOp) . words
    where
      putValue :: Integer -> Maybe [IExpr] -> Maybe [IExpr]
      putValue = fmap . (:) . Lit
      --
      performBinOp :: (IExpr -> IExpr -> IExpr) -> Maybe [IExpr] -> Maybe [IExpr]
      performBinOp op (Just (x : y : stack)) = Just $ (op x y) : stack
      performBinOp _ _ = Nothing
      --
      parseStackOp :: String -> Maybe (Maybe [IExpr] -> Maybe [IExpr])
      parseStackOp "+" = Just $ performBinOp Add
      parseStackOp "*" = Just $ performBinOp Mul
      parseStackOp value = (fmap putValue) . parse $ value
      --
      result :: Maybe [IExpr] -> Maybe IExpr
      result (Just [expr]) = Just expr
      result _ = Nothing

-- * Evaluation with parsing

-- | Parses given expression in Reverse Polish Notation and evaluates it
--
-- Returns 'Nothing' in case the expression could not be parsed.
--
-- Usage example:
--
-- >>> evaluateIExpr "2"
-- Just 2
-- >>> evaluateIExpr "2 3 +"
-- Just 5
-- >>> evaluateIExpr "3 2 * 3 +"
-- Just 9
-- >>> evaluateIExpr "2 +"
-- Nothing
-- >>> evaluateIExpr "2 3"
-- Nothing
evaluateIExpr :: String -> Maybe Integer
evaluateIExpr = (fmap evalIExpr) . parse
